plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.protobuf'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "id.co.rolllpdf"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            resValue("string","mobileads_app_id","ca-app-pub-3940256099942544~3347511713")
            resValue("string","banner_ad_unit_id","ca-app-pub-3940256099942544/6300978111")
            resValue("string","interstitial_ad_unit_id","ca-app-pub-3940256099942544/1033173712")
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue("string","mobileads_app_id","ca-app-pub-5236561708257284~3033893655")
            resValue("string","banner_ad_unit_id","ca-app-pub-5236561708257284/8506901553")
            resValue("string","interstitial_ad_unit_id","ca-app-pub-5236561708257284/1781780912")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
        }
    }
}

sourceSets {
    main.java.srcDirs += "${protobuf.generatedFilesBaseDir}/main/javalite"
    main.java.srcDirs += "$projectDir/src/main/proto"
}

processResources {
    exclude('**/*.proto')
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation 'androidx.activity:activity-ktx:1.3.0-alpha02'
    implementation 'androidx.fragment:fragment-ktx:1.3.0'

    implementation 'androidx.vectordrawable:vectordrawable:1.1.0'

    // CameraX
    implementation "androidx.camera:camera-core:1.1.0-alpha02"
    implementation "androidx.camera:camera-camera2:1.1.0-alpha02"
    implementation "androidx.camera:camera-view:1.0.0-alpha22"
    implementation "androidx.camera:camera-lifecycle:1.1.0-alpha02"
    implementation "androidx.camera:camera-extensions:1.0.0-alpha22"

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:2.31-alpha"
    kapt "com.google.dagger:hilt-android-compiler:2.31-alpha"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha03"

    //Live Cycle
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.0'

    // Coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.2"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.2"

    // Data Store Protobug altenate to shared preference
    implementation "androidx.datastore:datastore:1.0.0-alpha06"
    implementation 'com.google.protobuf:protobuf-lite:3.0.1'

    // Responsive Size
    implementation 'com.intuit.sdp:sdp-android:1.0.6'
    implementation 'com.intuit.ssp:ssp-android:1.0.6'

    // Glide (Image caching and management)
    def glideVersion = "4.11.0"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation "com.github.bumptech.glide:glide:$glideVersion"

    // Firebase & google
    implementation platform('com.google.firebase:firebase-bom:26.4.0')
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics'
    implementation 'com.google.firebase:firebase-appindexing'
    implementation 'com.android.billingclient:billing-ktx:3.0.2'
    implementation 'com.google.android.gms:play-services-ads:19.7.0'

    // Room
    implementation "androidx.room:room-runtime:2.2.6"
    implementation "androidx.room:room-ktx:2.2.6"
    kapt "androidx.room:room-compiler:2.2.6"

    // Database debug
    debugImplementation 'com.amitshekhar.android:debug-db:1.0.6'

    // Others
    implementation 'me.everything:overscroll-decor-android:1.1.0'
    implementation 'pub.devrel:easypermissions:3.0.0'
    implementation 'com.github.mukeshsolanki:photofilter:2.0.2'

    // Internal Library
    implementation project(":core")
    implementation project(":uikit")
    implementation project(":photocropper")


    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}